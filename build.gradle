buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(16)
        }
        withJavadocJar()
        withSourcesJar()
    }

    group = 'com.fnklabs.smsaero'
    version = "${version}"

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    ext {
        vers = [
                commonsio   : '1.3.2',

                httpclient  : '5.0.3',

                jackson     : '2.12.3',
                junit       : '5.7.0',
                jetbrains   : '21.0.1',
                logback     : '1.1.11',

                mockito     : '3.6.0',

                slf4j       : '1.1.11',

        ]
    }

    task allDeps(type: DependencyReportTask) {}

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:$vers.junit"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:$vers.junit"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$vers.junit"
        testImplementation "org.junit.vintage:junit-vintage-engine:$vers.junit"

        testImplementation "org.mockito:mockito-core:$vers.mockito"
        testImplementation "org.mockito:mockito-junit-jupiter:$vers.mockito"
    }
    test {
        minHeapSize = "512m"
        // Disable the test report for the individual test task
        reports.html.enabled = false

        failFast = false

        useJUnit()
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "short"
        }

    }



    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = 'smsaero-client'
                    description = 'SMS Aero client'
                    url = 'https://github.com/fnklabs/smsaero-client'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'alisher.alimov'
                            name = 'Alisher Alimov'
                            email = 'alisher.alimov@fnklabs.com'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:fnklabs/smsaero-client.git'
                        developerConnection = 'scm:git:ssh:git@github.com:fnklabs/smsaero-client.git'
                        url = 'https://github.com/fnklabs/smsaero-client'
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = "$sonatypeUsername"
                    password = "$sonatypePassword"
                }
            }
        }
    }

    signing {
        required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications.mavenJava
    }


    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
}
